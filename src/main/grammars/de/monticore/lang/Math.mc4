/*
 * ******************************************************************************
 * MontiCore Language Workbench, www.monticore.de
 * Copyright (c) 2017, MontiCore, All rights reserved.
 *
 * This project is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3.0 of the License, or (at your option) any later version.
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this project. If not, see <http://www.gnu.org/licenses/>.
 * ******************************************************************************
 */


/// NOTE: all vectors are row vectors to be compatible to Matlab and Octave
package de.monticore.lang;

grammar Math extends de.monticore.NumberUnit, de.monticore.AssignmentExpressions , de.monticore.CommonExpressions,
        de.monticore.common.Common, de.monticore.lang.Matrix {

    MathCompilationUnit =
        ("package" package:QualifiedName ";")?
        (ImportStatement)* // to import later on other scripts/functions
        MathScript;

    symbol scope MathScript =
        "script" Name (Expression)+ "end";

    //Arithmetic expressions for matrices

    MathArithmeticMatrixLeftDivideExpression implements Expression<160> =
    	Expression "\\" Expression;

    MathArithmeticMatrixTransposeExpression implements Expression<270> =
    	Expression ".\'";

    MathArithmeticMatrixComplexTransposeExpression implements Expression<270> =
    	Expression "\'";

    MathArithmeticMatrixSolutionExpression implements Expression<260> =
    	Expression "\\\\" Expression;

    MathArithmeticMatrixEEPowExpression implements Expression<290> =
    	Expression ".^" Expression;

    MathArithmeticMatrixEEMultExpression implements Expression<280> =
    	Expression "." "*" Expression;

    MathArithmeticMatrixEERightDivideExpression implements Expression<250> =
    	Expression "./" Expression;

    MathArithmeticMatrixEELeftDivideExpression implements Expression<250> =
    	Expression ".\\" Expression;

    MathDottedNameExpression implements Expression<300> =
        Name "." Name;

    MathMatrixNameExpression implements Expression<300> =
        Name "(" MathMatrixAccessExpression ")";

    //Boolean expressions
    MathTrueExpression implements Expression<240> =
    	"true";
    MathFalseExpression implements Expression<240> =
    	"false";

    //for loop
    MathForLoopExpression implements Expression<40> =
    	"for" head:MathForLoopHead body:Expression+ "end";

    MathForLoopHead =
    	Name "=" (NameExpression | Expression);

    //if  and else conditions
    MathConditionalExpression implements Expression<40> =
    	MathIfExpression MathElseIfExpression* MathElseExpression? "end";

    MathConditionalExpressionShort implements Expression<40> =
    	Expression "?" trueCase:Expression+ ":" falseCase:Expression+;

    MathIfExpression =
    	"if" condition:Expression body:Expression+ ;

    MathElseIfExpression =
    	"elseif" condition:Expression body:Expression+ ;

    MathElseExpression =
    	"else" body:Expression+ ;

    MathDeclarationExpression implements Expression<305> =
        type:AssignmentType Name;

    MathAssignmentDeclarationExpression implements Expression<306> =
        type:AssignmentType Name MathAssignmentOperator (Expression);

    MathAssignmentExpression implements Expression<200> =
        (Name | MathMatrixNameExpression | MathDottedNameExpression) MathAssignmentOperator (Expression);

    MathAssignmentOperator =
        operator:"=" | operator:"+=" | operator:"-=" | operator:"*=" | operator:"/=";

    // type definitions
    Range = "(" min:Expression ":" (step:Expression ":")? max:Expression ")";

    // R^2 for vector or R^{3,4} for Matrix
    Dimension =
        "^" ("{" matrixDim:(Expression || ",")+ "}" | vecDim:Expression);

    ElementType implements Type, TypeArgument =
        Name Range? Dimension?;

    //Assignments
    AssignmentType =
        matrixProperty:Name* ElementType;

    //Expression for all Numbers with units
    NumberExpression implements Expression<291> =
        NumberWithUnit;

    NameExpression implements Expression<295> =
        Name;

    Statement implements Expression<5> = Expression ";" ; // to not write down every time ";"?
 }
